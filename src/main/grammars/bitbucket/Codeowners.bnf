// https://mibexsoftware.atlassian.net/wiki/spaces/CODEOWNERS/pages/222822413/Usage
// playground: https://mibexsoftware.bitbucket.io/codeowners-playground/
{
  parserClass               = "com.github.fantom.codeowners.lang.kind.bitbucket.parser.CodeownersParser"
  extends                   = "com.github.fantom.codeowners.lang.CodeownersElementImpl"

  psiClassPrefix            = "Codeowners"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "com.github.fantom.codeowners.lang.kind.bitbucket.psi"
  psiImplPackage            = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl"

  elementTypeHolderClass    = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.CodeownersTypes"
  elementTypeClass          = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.CodeownersElementType"
  tokenTypeClass            = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.CodeownersTokenType"
//  tokenTypeClass            = "com.github.fantom.codeowners.lang.CodeownersTokenType"

  tokens = [
    WSS                     = "regexp:\s+"
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    SLASH                   = "/"
    AT                      = "@"
//    TEAMNAME                = "regexp:[\w-]+"
//    USERNAME                = "regexp:[\w\d-]+"
//    DOMAIN                  = "regexp:\w+(\.\w+)+"
//    VALUE                   = "regexp:[^@/\s]+"
    VALUE                   = "regexp:(?![!#\s])(?![\[\]])(?:\\[\[\]]|//|[^\[\]/\s])+"
    NAME_                   = "regexp:[^@/\s]+"
    // TODO maybe make it more precise, like https://stackoverflow.com/a/12093994/7286194
    BRANCH_PATTERN              = "regexp:\S+"
    //QUOTED_VALUE            = 'regexp:"([^@/]+)+"'
//    VALUES_LIST             = 'regexp:[^@/]+'
//    SPACES                  = 'regexp:\s+'
  ]

  name("Pattern.*")         = "pattern"
  implements("Pattern")     = "com.github.fantom.codeowners.lang.CodeownersPatternBase"
  mixin("Pattern")          = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl.CodeownersPatternExtImpl"
  implements("Rule")        = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.CodeownersRuleBase"
  mixin("Rule")             = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl.CodeownersRuleExtImpl"
//  mixin("NamedOwner")       = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl.CodeownersNamedOwnerExtImpl"
}

codeownersFile          ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | ConfigurationLine | value_item_ | CRLF
private value_item_     ::= (Rule | TeamDefinition) WSS? COMMENT?

ConfigurationLine              ::= DestinationBranchConfig | SubdirectoryOverridesConfig | CreatePullRequestCommentConfig
DestinationBranchConfig        ::= DESTINATION_BRANCH WSS BRANCH_PATTERN
SubdirectoryOverridesConfig    ::= SUBDIRECTORY_OVERRIDES WSS EnableOrDisable
CreatePullRequestCommentConfig ::= CREATE_PULL_REQUEST_COMMENT WSS EnableOrDisable
EnableOrDisable                ::= ENABLE | DISABLE

NEGATION                ::= "!"
Reset                   ::= NEGATION Pattern

TeamDefinition          ::= '@''@''@' TeamName WSS Owners {
  mixin = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl.CodeownersTeamDefinitionExtImpl"
}

Assign                  ::= Pattern /*SPACES*/ WSS Owners /*CRLF*/

Rule                    ::= Reset | Assign

Pattern                 ::= PatternDirectory | PatternFile

//QuotedEntry             ::= '"' <<entry_macro VALUES_LIST>> '"'
//private meta QuotedEntry::= '"' <<p>> '"'
//private meta entry_macro::= <<QuotedEntry <<p VALUES_LIST>> >> | <<p VALUE>>

//Entry                   ::= '/' ? <<list_macro VALUE>>
//private meta entry_file_raw ::= '/' ? <<list_macro <<p>>>>
//private meta entry_dir_raw  ::= '/' ? <<list_macro <<p>>>> '/'

PatternDirectory        ::= '/' ? <<list_macro VALUE>> '/' //| ('"' '/' ? <<list_macro VALUES_LIST>> '/' '"') //{ extends = "EntryFile"}
PatternFile             ::= '/' ? <<list_macro VALUE>> //| ('"' '/' ? <<list_macro VALUES_LIST>> '"')  //{ extends = "Entry"}
//EntryDirectory        ::= ('/' ? <<list_macro VALUE>> '/') | <<QuotedEntry '/' ? <<list_macro VALUES_LIST>> '/'>> //{ extends = "EntryFile"}
//EntryFile             ::= ('/' ? <<list_macro VALUE>>) | <<QuotedEntry '/' ? <<list_macro VALUES_LIST>> >> //{ extends = "Entry"}

Owners                  ::= Owner (WSS Owner)*
Owner                   ::= Email | NamedOwner
NamedOwner              ::= Team | User

Team                    ::= '@''@' TeamName {
//    mixin = "CodeownersTeamNameNamedElementImpl"
//    implements = "CodeownersTeamNameNamedElement"
    mixin = "com.github.fantom.codeowners.lang.kind.bitbucket.psi.impl.CodeownersNamedOwnerExtImpl"
//    methods = [ getName setName getNameIdentifier ]
}
User                    ::= '@' UserName

Email                   ::= UserName '@' Domain

// or make it token?
//QuotedValue             ::= '"'(CRLF VALUE)*'"'

UserName                ::= NAME_ //| QUOTED_VALUE
TeamName                ::= NAME_ //| QUOTED_VALUE
Domain                  ::= NAME_

private meta list_macro ::= <<p>> + ('/' <<p>> +) *